\subsection{Script CRBasic - Datalogger estacion Fundacion Chile}
\label{getData}
\begin{verbatim}
'CR800
'Manuel Arredondo for CR800 Estacion FCH Vitacura
'29/08/2012

'Declare Variables and Units
Public BattV
Public ApogeeFCH
Public SlrMJ
Public AirTC
Public RH

Public PTemp, batt_volt
Public socket, serial
Public GetRequest As String * 200
Public GetResponse As String * 200
Public httpGetMsg As String * 200
Public ipServer As String *200
Public varsGetRequest As String * 600

Units BattV=Volts
Units ApogeeFCH=W/m^2
Units SlrMJ=MJ/m^2
Units AirTC=Deg C
Units RH=%

'Define Data Tables
DataTable(solarFch,True,-1)
	DataInterval(0,1,Min,10)
	Sample(1,BattV,FP2)
	Sample(1,ApogeeFCH,FP2)
	Totalize(1,SlrMJ,IEEE4,False)
	Sample(1,AirTC,FP2)
	Sample(1,RH,FP2)
EndTable

'Main Program
BeginProg
	Scan(1,Min,1,0)
		'Def timestamp
		'TimeS = Status.timestamp(1,1)
		'TimeS = solarFch.TimeStamp

		'Default Datalogger Battery Voltage measurement BattV
		Battery(BattV)

		'Apogee Pyranometer measurements SlrMJ and ApogeeFCH
		VoltDiff(ApogeeFCH,1,mV250,1,True,0,_50Hz,1,0)
		If ApogeeFCH<0 Then ApogeeFCH=0
		SlrMJ=ApogeeFCH*0.012
		ApogeeFCH= ApogeeFCH*5.41

		'HMP50 Temperature & Relative Humidity Sensor measurements AirTC and RH
		VoltSe(AirTC,1,mV2500,3,0,0,_50Hz,0.1,-40)
		VoltSe(RH,1,mV2500,4,0,0,_50Hz,0.1,0)
		If (RH>100) AND (RH<108) Then RH=100
		
		'Call Data Tables and Store Data
		CallTable(solarFch)

		'peticion get al servidor de solaratacama.cl para registrar las lecturas en la BD
		ipServer = "www.solaratacama.cl"
		'varsGetRequest = "timestamp="+TimeS+"&battV="+BattV+"&radW="+Rad_W+"&radW2="+Rad_W_2+"&airTc="+AirTC+"&rh="+RH
		varsGetRequest = "battV="+BattV+"&radW="+ApogeeFCH+"&radW2="+SlrMJ+"&airTc="+AirTC+"&rh="+RH    
		GetRequest = "GET /solarDatos/getDatalogerFch.php?"+varsGetRequest+" HTTP/1.1"+CHR(13)+CHR(10)
		'GetRequest = "GET /getDatalogerFch.php HTTP/1.1"+CHR(13)+CHR(10)
		socket = TCPOpen(ipServer,80,1024)
		serial = SerialOpen(ComRS232,9600,0,20000,1000)
		if socket <> 0 Then
			SerialOut(socket,GetRequest,"",0,0)
			SerialOut(socket,"User-Agent:Mozilla/5.0"+CHR(13)+CHR(10),"",0,0)
			SerialOut(socket,"Host:"+ipServer+CHR(13)+CHR(10),"",0,0)
			SerialOut(socket,CHR(13)+CHR(10),"",0,0)
			SerialOut(serial,GetRequest,"0",1,150)
			SerialIn(GetResponse,socket,500,"",200)
			SerialOut(serial,GetResponse,"0",1,150)
		EndIf
	NextScan
EndProg
\end{verbatim}
